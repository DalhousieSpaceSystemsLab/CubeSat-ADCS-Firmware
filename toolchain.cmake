# CMAKE TOOLCHAIN FILE FOR msp430-elf-gcc
# AUTHOR: Carl Mattatall (cmattatall2@gmail.com) 

set(CODE_COMPOSER_INSTALL_PATH "/opt/ti/ccs1011/ccs")
set(MCU "msp430f5529")
set(MCU_FREQ "8000000")

if(NOT CODE_COMPOSER_INSTALL_PATH)
    message(FATAL_ERROR "CODE_COMPOSER_INSTALL_PATH NOT DEFINED. PLEASE DEFINE VIA CLI OR IN TOOLCHAIN FILE")
else()
    set(CCS_PATH "${CODE_COMPOSER_INSTALL_PATH}" CACHE STRING "Installation path for the code composer studio root folder")
endif()

if(UNIX AND NOT APPLE)
    set(TOOLCHAIN_ROOT ${CCS_PATH}//tools/compiler/msp430-gcc-9.2.0.50_linux64/)
    set(BIN_HINTS "${TOOLCHAIN_ROOT}/bin")
elseif(WIN32)
    # @todo
elseif(APPLE)
    # @todo
else()
    message(FATAL_ERROR "Unsupported platform ${CMAKE_HOST_SYSTEM_NAME}")
endif()

set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_SYSTEM_PROCESSOR MSP430)
set(CMAKE_CROSSCOMPILING ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_TRY_COMPILE_TARGET_TYPE "STATIC_LIBRARY")

set(TOOLCHAIN_PREFIX msp430-elf)
set(CMAKE_C_COMPILER_NAME ${TOOLCHAIN_PREFIX}-gcc)
set(CMAKE_ASM_COMPILER_NAME ${TOOLCHAIN_PREFIX}-gcc)
set(CMAKE_CXX_COMPILER_NAME ${TOOLCHAIN_PREFIX}-g++)
set(CMAKE_OBJCOPY_NAME ${TOOLCHAIN_PREFIX}-objcopy)
set(CMAKE_OBJDUMP_NAME ${TOOLCHAIN_PREFIX}-objdump)
set(CMAKE_SIZE_NAME ${TOOLCHAIN_PREFIX}-size)
set(CMAKE_GDB_NAME ${TOOLCHAIN_PREFIX}-gdb) 

if(NOT DEFINED BIN_HINTS)
    message(FATAL_ERROR "MSP430 SEARCH PATH HINTS ARE NOT DEFINED. PLEASE DEFINED THEM VIA THE CLI OR IN THE CMAKE TOOLCHAIN FILE FOR YOUR PLATFORM")
endif()

set(CMAKE_SYSROOT ${TOOLCHAIN_ROOT}/${TOOLCHAIN_PREFIX})
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
find_program(CMAKE_C_COMPILER ${CMAKE_C_COMPILER_NAME} HINTS ${BIN_HINTS} REQUIRED)
find_program(CMAKE_ASM_COMPILER ${CMAKE_ASM_COMPILER_NAME} HINTS ${BIN_HINTS} REQUIRED)
find_program(CMAKE_CXX_COMPILER ${CMAKE_CXX_COMPILER_NAME} HINTS ${BIN_HINTS} REQUIRED)
find_program(CMAKE_OBJCOPY ${CMAKE_OBJCOPY_NAME} HINTS ${BIN_HINTS} REQUIRED)
find_program(CMAKE_OBJDUMP ${CMAKE_OBJDUMP_NAME} HINTS ${BIN_HINTS} REQUIRED)
find_program(CMAKE_SIZE ${CMAKE_SIZE_NAME} HINTS ${BIN_HINTS} REQUIRED)

set(MCU_HEADER_DIR "${CCS_PATH}/ccs_base/msp430/include_gcc")
include_directories(${MCU_HEADER_DIR})

set(CMAKE_SHARED_FLAGS "-ffunction-sections -fdata-sections -DF_CPU=${MCU_FREQ} -mmcu=${MCU}")

set(CMAKE_C_FLAGS_INIT "${CMAKE_SHARED_FLAGS}"
     CACHE INTERNAL "Initial flags for C compiler")

set(CMAKE_CXX_FLAGS_INIT 
    "${CMAKE_SHARED_FLAGS} -fno-rtti -fno-exceptions" 
     CACHE INTERNAL "Initial flags for C++ compiler")

set(CMAKE_EXE_LINKER_FLAGS_INIT
    "-Wl,--relax,--gc-sections,-I${MCU_HEADER_DIR},-L${MCU_HEADER_DIR}"
    CACHE INTERNAL "Initial options for linker the VERY first time a CMake build tree is configured")
    
include(CheckLinkerFlag)

